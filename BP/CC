TCLREAD COMMAND
FILENAME = FIELD( COMMAND, ' ', 2 )
ITEMNAME = FIELD( COMMAND, ' ', 3 )
OPEN FILENAME TO F.FILE ELSE STOP 201, FILENAME
IF (ITEMNAME = '*') THEN
    EXECUTE "SSELECT " : FILENAME
    MAX.CC = 0
    MIN.CC = 99
    TOTAL.CC = 0
    ITEM.CNT = 0
    LOOP
        READNEXT ITEMNAME ELSE EXIT
        GOSUB CALC.CC
        IF (CC > MAX.CC) THEN MAX.CC = CC
        IF (CC < MIN.CC) THEN MIN.CC = CC
        TOTAL.CC += CC
        ITEM.CNT += 1
    REPEAT
    CRT
    CRT "Maximum Cyclomatic Complexity = " : MAX.CC
    CRT "Minimum Cyclomatic Complexity = " : MIN.CC
    CRT "Average Cyclomatic Complexity = " : (TOTAL.CC / ITEM.CNT)
END ELSE
    GOSUB CALC.CC
END
STOP

CALC.CC:
READ SRC FROM F.FILE, ITEMNAME ELSE STOP 202, ITEMNAME
SRC.LINES = DCOUNT( SRC, @FM )
CC = 1
FOR I = 1 TO SRC.LINES
    LINE = SRC<I>
    CALL COUNTKEYWORD( CNT, "THEN", LINE )
    CC += CNT
    CALL COUNTKEYWORD( CNT, "ELSE", LINE )
    CC += CNT
    CALL COUNTKEYWORD( CNT, "CASE", LINE )
    CC += CNT
    CALL COUNTKEYWORD( CNT, "BEGIN CASE", LINE )
    CC -= CNT
    CALL COUNTKEYWORD( CNT, "END CASE", LINE )
    CC -= CNT
    CALL COUNTKEYWORD( CNT, "FOR", LINE )
    CC += CNT
    CALL COUNTKEYWORD( CNT, "WHILE", LINE )
    CC += CNT
    CALL COUNTKEYWORD( CNT, "UNTIL", LINE )
    CC += CNT
NEXT I
CRT FILENAME : ", " : ITEMNAME : "     Cyclomatic Complexity = " : CC
RETURN
END
